apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'
apply plugin: 'com.jakewharton.hugo'
//apply plugin: 'com.getkeepsafe.dexcount'
apply from: "$project.rootDir/tools/quality.gradle"
apply from: "$project.rootDir/tools/script-git-version.gradle"

android {
  compileSdkVersion rootProject.COMPILE_SDK_VERSION
  buildToolsVersion rootProject.ANDROID_BUILD_TOOLS_VERSION
  defaultConfig {
    applicationId "com.yoloo.android"
    minSdkVersion rootProject.MIN_SDK_VERSION
    targetSdkVersion rootProject.TARGET_SDK_VERSION

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    // Enabling multidex support.
    multiDexEnabled true

    vectorDrawables.useSupportLibrary = true;
  }

  dexOptions {
    preDexLibraries true
    maxProcessCount 8
  }

  splits {
    abi {
      enable false
      reset()
      include 'x86', 'armeabi', 'armeabi-v7a', 'mips'
      universalApk true
    }
  }

  signingConfigs {
    // You must set up an environment var before release signing
    // Run: export APP_KEY={password}
    release {
      storeFile file('../keystore/release.keystore')
      keyAlias 'alias'
      storePassword "$System.env.APP_KEY"
      keyPassword "$System.env.APP_KEY"
    }

    debug {
      storeFile file('../keystore/debug.keystore')
      keyAlias 'androiddebugkey'
      storePassword 'android'
      keyPassword 'android'
    }
  }

  productFlavors {
    // Define separate dev and prod product flavors.
    dev {
      signingConfig signingConfigs.debug
      versionCode gitVersionCodeTime
      versionName gitVersionName
      // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
      // to pre-dex each module and produce an APK that can be tested on
      // Android Lollipop without time consuming dex merging processes.
      minSdkVersion 21
    }

    prod {
      signingConfig signingConfigs.release
      versionCode gitVersionCode
      versionName gitVersionName
      // The actual minSdkVersion for the application.
      minSdkVersion 16
    }
  }

  buildTypes {
    release {
      debuggable false
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'),
          "$project.rootDir/tools/rules-proguard.pro"
      signingConfig signingConfigs.release
      buildConfigField "String", "SERVER_URL", '"https://yoloo-151719.appspot.com/_ah/api/"'
    }

    debug {
      versionNameSuffix ".debug"
      signingConfig signingConfigs.debug
      buildConfigField "String", "SERVER_URL", '"https://yoloo-151719.appspot.com/_ah/api/"'
    }

    qa {
      versionNameSuffix ".qa"
      buildConfigField "String", "SERVER_URL", '"https://yoloo-151719.appspot.com/_ah/api/"'
    }
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

/* Forces all transitive deps listed to our version. */
configurations.all {
  resolutionStrategy.force rootProject.deps.androidSupportLibrary,
      rootProject.deps.androidCardView, rootProject.deps.androidDesignLibrary,
      rootProject.deps.androidRecyclerView, rootProject.deps.androidSupportCompat,
      rootProject.deps.androidCustomTabs, rootProject.deps.androidAnnotations,
      rootProject.deps.junit, rootProject.deps.okhttp, 'com.google.code.findbugs:jsr305:3.0.1'
}

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')
  endpointsServer project(path: ':backend', configuration: 'endpoints')
  compile project(':chatkit')
  compile('com.google.api-client:google-api-client-android:1.22.0') {
    exclude group: 'org.apache.httpcomponents', module: 'httpclient'
  }

  compile rootProject.deps.androidSupportLibrary
  compile rootProject.deps.androidRecyclerView
  compile rootProject.deps.androidDesignLibrary
  compile rootProject.deps.androidCardView
  compile rootProject.deps.androidSupportCompat
  compile rootProject.deps.androidPalette
  compile rootProject.deps.androidCustomTabs
  compile rootProject.deps.androidMultidex
  compile rootProject.deps.androidConstraintLayout

  //compile "com.google.firebase:firebase-core:$FIREBASE_VERSION"
  compile rootProject.deps.firebaseAuth
  compile rootProject.deps.firebaseMessaging
  compile rootProject.deps.firebaseInvites
  compile rootProject.deps.firebaseDatabase
  compile rootProject.deps.firebaseCrash

  compile rootProject.deps.googlePlayAuth

  compile rootProject.deps.conductor
  compile rootProject.deps.conductorSupport

  compile rootProject.deps.okhttp
  compile 'com.squareup.okhttp3:okhttp-urlconnection:3.6.0'

  compile rootProject.deps.butterknifeLibrary
  annotationProcessor rootProject.deps.butterknifeCompiler

  compile rootProject.deps.rxjava2
  compile rootProject.deps.rxjavaAndroid

  compile rootProject.deps.epoxyLibrary
  annotationProcessor rootProject.deps.epoxyProcessor

  compile rootProject.deps.glide

  compile 'com.facebook.android:facebook-android-sdk:4.20.0'

  compile 'com.hootsuite.android:nachos:1.0.0'

  compile 'com.jakewharton.timber:timber:4.5.1'

  compile 'com.yalantis:ucrop:2.2.0'

  compile 'com.github.jksiezni.permissive:permissive:0.2'
  compile('com.sandrios.android:sandriosCamera:1.0.9', {
    exclude group: 'gun0912.ted'
  })
  compile 'com.github.chrisbanes:PhotoView:1.3.1'
  compile 'com.beloo.widget:ChipsLayoutManager:0.3.7@aar'

  compile 'com.annimon:stream:1.1.6'
  compile 'com.github.adhishlal:asdp:1.1'
  compile 'uk.co.chrisjenx:calligraphy:2.2.0'
  compile 'com.airbnb.android:lottie:1.5.3'
  compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
  compile 'com.ogaclejapan.arclayout:library:1.1.0@aar'
  compile 'com.google.android:flexbox:0.3.0-alpha2'
  compile 'com.firebase:firebase-jobdispatcher-with-gcm-dep:0.5.2'

  annotationProcessor 'dk.ilios:realmfieldnameshelper:1.1.0'

  debugCompile "com.github.brianPlummer:tinydancer:0.1.1"
  debugCompile 'com.facebook.stetho:stetho:1.4.2'
  debugCompile 'com.uphyca:stetho_realm:2.0.0'

  testCompile 'com.google.code.findbugs:jsr305:3.0.1'
  testCompile 'junit:junit:4.12'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}

// Put google_services.json
apply plugin: 'com.google.gms.google-services'